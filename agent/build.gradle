plugins {
    id 'java'
}

dependencies {
    compileOnly "junit:junit:4.12"
    compileOnly 'org.testng:testng:6.8'
    compile 'net.bytebuddy:byte-buddy:1.7.9'
    compileOnly 'net.bytebuddy:byte-buddy-agent:1.7.9'
    runtime 'net.bytebuddy:byte-buddy-agent:1.7.9'
    testCompileOnly "junit:junit:4.12"
    testRuntimeOnly "junit:junit:4.12"
    compile 'org.objenesis:objenesis:2.6'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task copyMockKDispatcher(type: Copy) {
    dependsOn compileJava
    from "${sourceSets.main.java.outputDir}/io/mockk/proxy"
    into "${sourceSets.main.java.outputDir}/io/mockk/proxy"
    include "MockKDispatcher.class"
    include "MockKWeakMap.class"
    include "MockKWeakMap\$StrongKey.class"
    include "MockKWeakMap\$WeakKey.class"
    rename { String fileName ->
        fileName.replace('.class', '.clazz')
    }
}
classes.dependsOn(copyMockKDispatcher)

jar {
    exclude "io/mockk/proxy/MockKDispatcher.class"
    exclude "io/mockk/proxy/MockKWeakMap*.class"
}

artifacts {
    archives sourcesJar
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
            }

            pom.project {
                name 'MockK Java Agent'
                packaging 'jar'
                description 'MockK agent to remove finals'
                url 'http://mockk.io'

                scm {
                    connection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    developerConnection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    url 'http://www.github.com/oleksiyp/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}
